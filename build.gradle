apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'

repositories {
    mavenCentral()
}

dependencies {
    compile "org.seleniumhq.selenium:selenium-java:2.21.0"
    compile "org.testng:testng:6.3.1"
    testCompile "com.google.inject:guice:3.0"
    testCompile("org.uncommons:reportng:1.1.2") {
        exclude group: "org.testng", module: "testng"
    }
}

tasks.withType(Test) {
    useTestNG()
    systemProperties = System.getProperties()
    maxParallelForks = 5
    options {
        listeners << 'org.uncommons.reportng.HTMLReporter'
        listeners << 'org.uncommons.reportng.JUnitXMLReporter'
    }
    //makes the standard streams (err and out) visible at console when running tests
    testLogging.showStandardStreams = true
    //listening to test execution events
    beforeTest { descriptor ->
        logger.lifecycle("Running test: " + descriptor)
    }
    onOutput { descriptor, event ->
        logger.lifecycle("Test: " + descriptor + " produced standard out/err: " + event.message )
    }
}

task includegroup1(type: Test) {
    useTestNG() {
        includeGroups 'group1'
    }
}

task excludegroup1(type: Test) {
    useTestNG() {
        excludeGroups 'group1'
    }
}


